ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

********************************************************************************
** Данный скрипт представляет собой набор функций и средств,
** которые могут использоваться многими другими скриптами.
********************************************************************************

** FU7700 - вызывается каждый раз при начале новой игры или при загрузке старой.
** Вместо использования триггеров PI и GM0 в каждом скрипте для написания одного и
** того же кода (например UN:C-ресиверов), можно просто использовать эту функцию.
!?PI; !!FU7700:P; !?GM0; !!FU7700:P; !?FU7700; !!FU:E;
********************************************************************************

** Автор: igrik
** FU7701 - позволяет экономить z-переменные, устанавливая в качестве описаний
** навыков, монстров и артефактов значения z-переменных из ers-файлов
** Описания нужно обновлять после загрузки старой игры (например, в триггере GM0).
** Вернуть стандартные описания можно штатным функционалом ресивера UN:G.
** Параметры функции:
** x1 - тип функции: 0 - втор.навык
**   x2 - номер навыка [0...27]
**   x3 - название,уровень навыка [0...3]
** x1 - тип функции: 1 - навзания и описания монстров
**   x2 - номер монстра [0...196]
**   x3 - тип [0...2] как и в UN:G1
** x1 - тип функции: 2 - специализация героя
**   x2 - номер героя [0...155]
**   x3 - тип всегда 2
** x1 - тип функции: 3 - артефакт
**   x2 - номер артефакта [0...170]
**   x3 - тип: 0 - название, 1 - описание
** x4 - номер z переменной из ert файла

!?FU7701; [функция установки описания прямо из ert]
!!SN:E7824928/1/x4; !!VRy1:Sv1;
!!if&x1=0/x2<28/x3<4:;
!!VRy2:Sx2 *16; !!VRy3:Sx3 *4 +6917512 +y2; !!UN:Cy3/4/y1;
!!en:;
!!if&x1=1/x2<197:;
!!VRy4&x3<1:S20; !!VRy4&x3=1:S24; !!VRy4&x3>1:S28; !!UN:C6768560/4/?y2; !!VRy3:Sx2 *116 +y2 +y4; !!UN:Cy3/4/y1;
!!en:;
!!if&x1=2/x2<155/x3=2:;
!!UN:C6790272/4/?y4; !!VRy5:Sx2 *40; !!VRy6:Sy4 +36 +y5; !!UN:Cy6/4/y1;
!!en:;
!!if&x1=3/x2<171:;
!!VRy4&x3<>0:S16; !!UN:C6687592/4/?y2; !!VRy3:Sx2 *32 +y2 +y4; !!UN:Cy3/4/y1;
!!en:;
********************************************************************************

** FU7702 - изменяет описание специализации у всех героев, которые ею обладают.
** x1 - номер z-переменной из ert-файла содержащей описание специализации,
** x2..x8 - параметры специализации, соответствуют параметрам 1..7 ресивера HE:X.
!?FU7702; !!DO7790/0/155/1:Px1/x2/x3/x4/x5/x6/x7/x8;
!?FU7790; !!HEx16:X?y2/?y3/?y4/?y5/?y6/?y7/?y8; !!FU7701&x2=y2/x3=y3/x4=y4/x5=y5/x6=y6/x7=y7/x8=y8:P2/x16/2/x1;
********************************************************************************

** Автор: Демиург, Берсеркер
** FU7703 - устанавливает вещественное значение для вторичного навыка
** x1 - навык, x2 - уровень (0..3), x3 - значение в прцоентах, например 8
!?FU7703; !!FU|x1<1/x1=5/x1=7/x1=10/x1>26:E; !!FU&x1>13/x1<21:E; !!FU|x2<0/x2>3:E;
!!VRy1&x1=1:S6547944;  !!VRy1&x1=2:S6548072;  !!VRy1&x1=3:S6547928;  !!VRy1&x1=4:S6548024;
!!VRy1&x1=6:S6547880;  !!VRy1&x1=8:S6547912;  !!VRy1&x1=9:S6547864;  !!VRy1&x1=11:S6548008;
!!VRy1&x1=12:S6547896; !!VRy1&x1=13:S6547992; !!VRy1&x1=21:S6548056; !!VRy1&x1=22:S6547960;
!!VRy1&x1=23:S6547976; !!VRy1&x1=24:S6548104; !!VRy1&x1=25:S6548088; !!VRy1&x1=26:S6548040;
!!VRx2:*4; !!VRy1:+x2; !!VRe1:Sx3:100; !!SN:X?y2 Xe1 X?y3 Xy2; !!UN:Cy1/4/y3;
********************************************************************************

** FU777x - новые триггеры. Автор - igrik
!?FU7700;                                [установка хуков в начале игры и после загрузки]
!!SN:L^EraPlugins\erm_hooker.era^/?y1;   [проверка плагина]
!!FU&y1=0:E;                             [выход, если плагина нет]
!!SN:Ay1/^SetHook^/?y2;                  [y2 - адрес функции SetHook]
!!SN:Ey2/1/5909146/7770;   /0х5A2A9A/    [хук в функции CastSpell]
!!SN:Ey2/1/5090553/7771;   /0x4DACF9/    [диалог повышения уровня героя без выбора втор. навыков (0x4DAD70/0x4DAE84 - 1/2 навыка)]
!!SN:Ey2/1/4609014/7772;   /0x4653F6/    [после фазы регенерации; проверки на мораль, страх; и после установки активного стека]
!!SN:Ey2/1/4462998/7773;                 [после атаки]
!!SN:Ey2/1/4455129/7774;                 [после выстрела]
!!SN:Ey2/1/7710213/7775;                 [перед битвой перед триггером BA0]
!!SN:Ey2/1/5105173/7767;                 [перед переключением между героями в окне героя]
!!SN:Ey2/1/6027880/7776;   /0x5BFA68/    [расчет дохода города]
!!SN:Ey2/1/5011264/7768;                 [AddGoldForArt]
!!SN:Ey2/1/4950687/7769;                 [AddResForArt]


!?FU7770;                                [триггер: получение отрядом магического урона]
!?FU7771;                                [диалог повышения уровня героя без выбора втор. навыков]
!?FU7772;                                [триггер: после фазы регенерации; проверки на мораль, страх; и после установки активного стека]
!?FU7773;                                [после атаки]
!?FU7774;                                [после выстрела]
!?FU7775;                                [перед битвой перед триггером BA0]
!?FU7776;                                [расчет дохода города]
!?FU7767;                                [перед переключением между героями в окне героя]
!?FU7768;                                [AddGoldForArt]
!?FU7769;                                [AddResForArt]

********************************************************************************
